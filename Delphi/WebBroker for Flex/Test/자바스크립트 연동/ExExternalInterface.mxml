<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" width="600" height="350" creationComplete="init()">

	<mx:Script>
		<![CDATA[
			public function init():void
			{
				txtMsg.text += "초기화중...\r";
				if( ExternalInterface.available )
				{
					ExternalInterface.addCallback("sendToActionScript", receivedFromJavaScript);
					if( checkJavaScriptReady())
					{
						txtMsg.text += "Javascript가 준비됨\r";
					}
					else
					{
						txtMsg.text += "JavaScript가 준비되지 못했음 대기 \n";
                        var readyTimer:Timer = new Timer(100, 0);
                        readyTimer.addEventListener(TimerEvent.TIMER, timerHandler);
                        readyTimer.start();						
					}
				}
				else
				{
					txtMsg.text += "External Interface를 사용할 수 없음\r";
				}
			}		
			
			private function receivedFromJavaScript(value:String):void 
			{
            	txtMsg.text += "JavaScript says: " + value + "\r";
   			}
   			
			private function checkJavaScriptReady() :Boolean 
			{
            	var isReady:Boolean = ExternalInterface.call("isReady");
            	return isReady;
        	}
        	 
			private function timerHandler(event:TimerEvent) :void 
			{
				txtMsg.text += "자바스크립트 상태 Check중...\r";
				var isReady:Boolean = checkJavaScriptReady();
				if (isReady) 
				{
					txtMsg.text += "JavaScript가 준비됨. \r";
					Timer(event.target).stop();
				}
			}
        	  
   			private function clickHandler(event:MouseEvent) :void 
   			{
	            if (ExternalInterface.available) 
	            {
	                ExternalInterface.call("sendToJavaScript", txtName.text);
	                txtName.text ="";
	            }
        	}
		]]>
	</mx:Script>
	<mx:Style>
		Application 
		{
			fontSize:12pt;
		}
	</mx:Style>	
	<mx:TextInput x="10" y="10" id="txtName" width="189.5"/>
	<mx:Button x="207.5" y="10" label="자바스크립트로 보내기" id="btnSendToJS" width="146" height="24" click="clickHandler(event)"/>
	<mx:TextArea id="txtMsg" x="10" y="57" text="text" width="580" height="283"/>
	
</mx:Application>
